apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def env = System.getenv('BUILD_ENV')

if(env == null || (env != null && env != 'Travis')) {
    def libName = 'peko'
    def libGroup = 'com.markodevcic.' + libName
    def libSite = 'https://github.com/deva666/' + libName
    def libGit = libSite
    def libTracker = libSite + '/issues'
    def libDesc = "Android Library for requesting Permissions with Kotlin Coroutines"
    def libTags = ['android', 'android-permissions', 'kotlin-coroutines', 'kotlin']

    group libGroup
    version '2.1.2'

    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty('BINTRAY_USER')
        key = properties.getProperty('BINTRAY_API_KEY')
        configurations = ['archives']
        pkg {
            repo = 'maven'
            name = libName
            desc = libDesc
            websiteUrl = libSite
            issueTrackerUrl = libTracker
            vcsUrl = libGit
            labels = libTags
            licenses = ['Apache-2.0']
            publish = true
            publicDownloadNumbers = true
        }
    }

    install {
        repositories.mavenInstaller {
            pom.project {
                packaging 'aar'
                groupId libGroup
                artifactId libName
                name libName
                description libDesc
                url libSite
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dev-m'
                        name 'Marko Devcic'
                        email 'madevcic@gmail.com'
                    }
                }
                scm {
                    connection libGit
                    developerConnection libGit
                    url libSite
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier 'sources'
    }

    artifacts {
        archives sourcesJar
    }
}
